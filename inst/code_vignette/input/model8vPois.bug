###########################################################################
# Parameter distributions are given as discrete across
# a fine grid since these are non-conjugate models e.g. parameters
# may not conform to a standard probability distribution
#
##########################################################################
# DAG Definition. rows are children, cols are parents row are children
##########################################################################
# AR pneumS female livdam eggs worms age adg
# 0      0      0      0    0     0   1   0   AR|age
# 0      0      0      0    0     0   1   0   pneumS|age
# 0      0      0      0    0     0   0   0   female  
# 0      0      0      0    1     0   0   0   livdam|eggs  
# 0      0      0      0    0     0   0   1   eggs|adg    
# 1      0      0      0    1     0   1   1   wormCount|AR:eggs:age:adg   
# 0      0      1      0    0     0   0   0   age|female     
# 0      0      0      0    0     0   1   0   adg|age        

model {    

       # BINARY NODES

       #- NODE AR|age -------------------------------------------------------
       AR ~ dbern(pAR); # binary response 
       logit(pAR)<- AR.c0 + AR.c1*age;   # logistic regression

       ### empirical densities from abn fitabn()
       AR.M0 ~ dcat(AR.p[ ,2]);   # AR.p[,2] is the vector of density values= f(x)
       AR.c0 <- AR.p[AR.M0,1];    # AR.p[,1] is the vector of x values
       AR.M1 ~ dcat(AR.p[ ,4]);   # AR.p[,4] is the vector of density values= f(x)
       AR.c1 <- AR.p[AR.M1,3];    # AR.p[,3] is the vector of x values
       
       #-----------------------------------------------------------------------

       #- pneumS|age ----------------------------------------------------------
       pneumS  ~ dbern(ppneumS); 
       logit(ppneumS)<- pneumS.c0 + pneumS.c1*age; 
       
       pneumS.M0 ~ dcat(pneumS.p[ ,2]);   
       pneumS.c0 <- pneumS.p[pneumS.M0,1];   
       pneumS.M1 ~ dcat(pneumS.p[ ,4]);   
       pneumS.c1 <- pneumS.p[pneumS.M1,3];   
   
       #-----------------------------------------------------------------------
       
       #- NODE female ---------------------------------------------------------
       female  ~ dbern(pfemale); 
       logit(pfemale)<- female.c0 ; 
       
       female.M0 ~ dcat(female.p[ ,2]);   
       female.c0 <- female.p[female.M0,1];   
       #-----------------------------------------------------------------------

       
       #- NODE livdam|eggs ----------------------------------------------------
       livdam  ~ dbern(plivdam); 
       logit(plivdam)<- livdam.c0 + livdam.c1*eggs; 
       
       livdam.M0 ~ dcat(livdam.p[ ,2]);   
       livdam.c0 <- livdam.p[livdam.M0,1];   
       livdam.M1 ~ dcat(livdam.p[ ,4]);    
       livdam.c1 <- livdam.p[livdam.M1,3];    
       #-----------------------------------------------------------------------

       
       #- NODE eggs|adg -------------------------------------------------------
       eggs ~ dbern(peggs); 
       logit(peggs)<- eggs.c0 + eggs.c1*adg;

       eggs.M0 ~ dcat(eggs.p[ ,2]);  
       eggs.c0 <- eggs.p[eggs.M0,1];   
       eggs.M1 ~ dcat(eggs.p[ ,4]);   
       eggs.c1 <- eggs.p[eggs.M1,3];    
  
       #------------------------------------------------------------------------

       # POISSON NODE
       
       #- NODE wormCount|AR:eggs:age:adg -------------------------------------------------------
       wormCount  ~ dpois(pwormCount); 
       log(pwormCount)<- wormCount.c0 + wormCount.c1*AR + wormCount.c2*eggs + wormCount.c3*age + wormCount.c4*adg; 
       
       wormCount.M0 ~ dcat(wormCount.p[ ,2]);   
       wormCount.c0 <- wormCount.p[wormCount.M0,1];   
       wormCount.M1 ~ dcat(wormCount.p[ ,4]);    
       wormCount.c1 <- wormCount.p[wormCount.M1,3];    
       wormCount.M2 ~ dcat(wormCount.p[ ,6]);    
       wormCount.c2 <- wormCount.p[wormCount.M1,5];
       wormCount.M3 ~ dcat(wormCount.p[ ,8]);    
       wormCount.c3 <- wormCount.p[wormCount.M1,7]; 
       wormCount.M4 ~ dcat(wormCount.p[ ,10]);    
       wormCount.c4 <- wormCount.p[wormCount.M1,9]; 
       #-----------------------------------------------------------------------

       # GAUSSIAN NODES 
       
       #- NODE age|female -----------------------------------------------------
       age  ~ dnorm(mu.age,prec.age);  
       mu.age<- age.c0 + age.c1*female;

       age.M0 ~ dcat(age.p[ ,2]);   
       age.c0  <- age.p[age.M0,1];   
       age.M1 ~ dcat(age.p[ ,4]);   
       age.c1  <- age.p[age.M1,3];   

       prec.age.M ~ dcat(prec.age.p[ ,2]);    
       prec.age <-  prec.age.p[prec.age.M,1];   
       #-----------------------------------------------------------------------    
       
       
       #- NODE adg|age --------------------------------------------------------
       adg  ~ dnorm(mu.adg,prec.adg);  
       mu.adg <- adg.c0 + adg.c1*age;

       adg.M0 ~ dcat(adg.p[ ,2]);   
       adg.c0  <- adg.p[adg.M0,1];   
       adg.M1 ~ dcat(adg.p[ ,4]);   
       adg.c1  <- adg.p[adg.M1,3];   

       prec.adg.M ~ dcat(prec.adg.p[ ,2]);    
       prec.adg <-  prec.adg.p[prec.adg.M,1];   
       #-----------------------------------------------------------------------    


     
}